buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'de.marcphilipp.nexus-publish' version '0.2.0'
}

apply plugin: 'java'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = 'com.github.leeonky'
version = '0.0.2' + System.env.VERSION_SCOPE
sourceCompatibility = 1.8

jacocoTestReport {
    reports {
        xml.enabled = true // for coveralls
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.assertj:assertj-core:3.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/leeonky/' + project.name

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'leeonky'
                        name = 'Liangchen'
                        email = 'leeonky@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/leeonky/' + project.name + '.git'
                    developerConnection = 'scm:git:git@github.com:leeonky' + project.name + '.git'
                    url = 'https://github.com/leeonky/' + project.name + '.git'
                }
            }
        }
    }
}

nexusPublishing {
    username = System.env.ossrhUsername
    password = System.env.ossrhPassword
}

task 'checkTag' {
    if (System.env.TRAVIS_TAG != project.version) {
        throw new InvalidUserDataException("Tag and version does not match, expect '" + version + "', but '" + System.env.TRAVIS_TAG + "'")
    }
}

publishToNexus {
    dependsOn 'checkTag'
}
